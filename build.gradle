/*
 * Copyright (c) 2009-2019 digi.me Limited. All rights reserved.
 */

apply from: 'config/gradle/versioning.gradle'

apply from: 'config/gradle/checkstyle.gradle'

buildscript {
    // Gradle will not find vars defined in an external file when referring to them
    // in the buildscript block, unless you link it from the buildscript block, too.
    apply from: 'config/gradle/versioning.gradle'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidGradlePlugin"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:$versions.buildInfoExtractor"
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

allprojects {
    group = 'me.digi'
    repositories {
        google()
        jcenter()
        maven { url "https://repository.sysdigi.net/m2/libs-release" }
    }
    project.ext {
        globalVersion = "unspecified"
    }
}

def projectVersion() {
    def versionClass = file('sdk/src/main/java/me/digi/sdk/core/DigiMeSDKVersion.java')
    def outVersion = null
    versionClass.eachLine{
        def regex = (it =~ /(?:.*VERSION = \")(.*)(?:\".*)/)
        if (regex.matches()) {
            outVersion = regex[0][1]
        }
    }
    return outVersion
}

ext.VERSION_NAME = projectVersion()

println "SDK version is ${ext.VERSION_NAME}"

def updateReadmeVersion() {
    //UPDATE readme only when dev is working with new production version
    def appVersionName = ext.VERSION_NAME
    if (appVersionName.contains("SNAPSHOT"))
        return
    println "Updating readme file with $appVersionName"
    def readmeFile = file('README.md')
    StringBuilder outContent = new StringBuilder()
    boolean shouldWrite = false
    readmeFile.eachLine {
        def regex = (it =~ /(?:.*me.digi:sdk:)(.*)(?:\'.*)/)
        if (regex.matches() && !appVersionName.equals(regex[0][1])) {
            shouldWrite = true
            outContent.append(it.replace(regex[0][1], appVersionName)).append("\n")
        } else {
            outContent.append(it).append("\n")
        }
    }
    if (shouldWrite) {
        readmeFile.write(outContent.toString().trim(), "UTF-8")
    }
}

updateReadmeVersion()

task clean(type: Delete) {
    delete rootProject.buildDir
}
