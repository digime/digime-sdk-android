apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'signing'
apply plugin: 'maven'
apply from: 'config/gradle/versioning.gradle'
apply from: 'config/properties/publishing.properties'


apply from: 'config/gradle/maven-publish-helper.gradle'

publishing {
    publications {
        release(MavenPublication) {

            // Specify own groupId as package name of your library,
            groupId 'me.digi'

            // Specify custom artifactId if needed,
            artifactId = 'sdk'

            // You can specify custom version,
            version = android.defaultConfig.versionName
        }
    }

    signing {
        sign publishing.publications.release
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.allTasks.any { it instanceof Sign }) {
            allprojects {
                ext."signing.keyId" = project.ext.SIGNING_KEY_ID
                ext."signing.secretKeyRingFile" = project.ext.SIGNING_KEYRING_PATH
                ext."signing.password" = project.ext.SIGNING_KEY_PASS
            }
        }
    }

    tasks.register("artifactoryPublish", GradleBuild.class) {
        buildName = "digime"

        group = 'publishing'
        description = 'Prepares a clean release build and publishes to the appropriate repository.'

        def version = android.defaultConfig.versionName

        def publishTask

        if (version.endsWith('SNAPSHOT')) {
            // Deploy to snapshot repo.
            publishTask = 'publishAllPublicationsToDigiMeSnapshotRepository'
        } else {
            // Deploy to main repo.
            publishTask = 'publishAllPublicationsToDigiMeReleaseRepository'
        }

        tasks = ['clean', 'assembleRelease', publishTask]
    }
}