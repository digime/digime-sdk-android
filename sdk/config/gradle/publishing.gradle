apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'signing'
apply plugin: 'maven'
apply from: 'config/gradle/versioning.gradle'
apply from: 'config/properties/publishing.properties'

publishing {

    publications {

        sdk(MavenPublication) {

            // Include components.
            if (components.hasWithName("java")) {
                from components.java
            } else if (components.hasWithName("android")) {
                from components.android
            }

            // Configure core metadata.
            groupId = 'me.digi'
            artifactId = 'sdk'
            version = android.defaultConfig.versionName

            // Configure extra POM metadata.
            pom {

                name = 'DigiMeSDK'
                description = 'digi.me Private Sharing SDK for Android'
                packaging = 'aar'

                licenses {

                    license {

                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

                    }

                }

                developers {

                    developer {
                        id = 'JacobKingDev'
                        name = 'Jacob King'
                        email = 'jacob@digi.me'
                    }

                }

                scm {

                    connection = 'scm:git:git://github.com/digime/digime-sdk-android.git'
                    developerConnection = 'scm:git:git://github.com/digime/digime-sdk-android.git'
                    url = 'https://github.com/digime/digime-sdk-android/'

                }

            }

        }

    }

    repositories {

        maven {

            name = 'DigiMeSnapshot'
            url = 'https://repository.sysdigi.net/m2/libs-snapshot-local/'

            credentials {
                username = project.ext.DIGIME_REPO_USER
                password = project.ext.DIGIME_REPO_PASS
            }

        }

        maven {

            name = 'DigiMeRelease'
            url = 'https://repository.sysdigi.net/m2/libs-release-local/'

            credentials {
                username = project.ext.DIGIME_REPO_USER
                password = project.ext.DIGIME_REPO_PASS
            }

        }
    }
}

signing {
    sign publishing.publications.sdk
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects {
            ext."signing.keyId" = project.ext.SIGNING_KEY_ID
            ext."signing.secretKeyRingFile" = project.ext.SIGNING_KEYRING_PATH
            ext."signing.password" = project.ext.SIGNING_KEY_PASS
        }
    }
}

tasks.register("updateInstallationGuideVersion") {

    group = 'publishing'

    doLast {
        def appVersionName = android.defaultConfig.versionName
        def gettingStartedFile = new File('./docs/guides/Getting Started.md')
        StringBuilder outContent = new StringBuilder()
        boolean shouldWrite = false

        gettingStartedFile.eachLine {
            def regex = (it =~ /(?<=implementation "me\.digi:sdk:)(\d\.\d\.\d)(?=")/)
            if (regex.find() && regex.size() > 0 && appVersionName != regex[0][1]) {
                shouldWrite = true
                outContent.append(it.replace(regex[0][1], appVersionName)).append("\n")
            } else {
                outContent.append(it).append("\n")
            }
        }

        if (shouldWrite) {
            gettingStartedFile.write(outContent.toString().trim(), "UTF-8")
            new File('./README.md').bytes = gettingStartedFile.bytes
        }
    }
}

tasks.register("generateDocs") {

    group = 'publishing'

    './docs/generateDocs.sh'.execute()
}

tasks.register("artifactoryPublish", GradleBuild.class) {

    group = 'publishing'
    description = 'Prepares a clean release build and publishes to the appropriate repository.'

    def version = android.defaultConfig.versionName

    def publishTask

    if (version.endsWith('SNAPSHOT')) {
        // Deploy to snapshot repo.
        publishTask = 'publishAllPublicationsToDigiMeSnapshotRepository'
    }
    else {
        // Deploy to main repo.
        publishTask = 'publishAllPublicationsToDigiMeReleaseRepository'
    }

    tasks = ['clean', 'assembleRelease', 'sourcesJar', 'updateInstallationGuideVersion', 'updateDocs', publishTask]
}

tasks.register("localPublish", GradleBuild.class) {

    group = 'publishing'
    description = 'Prepares a clean release build and publishes to maven local.'

    tasks = ['clean', 'assembleRelease', 'publishToMavenLocal']
}