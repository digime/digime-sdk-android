/*
 * Copyright (c) 2009-2019 digi.me Limited. All rights reserved.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: project.file('../config/gradle/quality.gradle')
apply from: project.file('../config/gradle/config-environment.gradle')

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionName VERSION_NAME
        versionCode 1

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }
    
    buildTypes {
        debug {
            buildConfigField "String", "BASE_HOST",  "\"${exports.endpointUrl}\""
            buildConfigField "boolean", "LOG_REQUESTS",  "true"
        }

        release {
            buildConfigField "String", "BASE_HOST", "\"${exports.endpointUrl}\""
            buildConfigField "boolean", "LOG_REQUESTS",  "false"
        }
    }

    lintOptions {
        lintConfig project.file('../gradle/lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }
}

dependencies {
    implementation project(path: ':sdk-lib-crypto')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"

    implementation "com.android.support:support-annotations:$versions.supportLibrary"

    implementation "com.google.code.gson:gson:$versions.gson"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "org.apache.commons:commons-compress:$versions.commonsCompress"
    implementation "org.brotli:dec:$versions.brotli"

    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"
    testImplementation "io.mockk:mockk:$versions.mockk"

    androidTestImplementation "org.mockito:mockito-core:$versions.mockito"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"
    androidTestImplementation "com.android.support:support-annotations:$versions.supportLibrary"
    androidTestImplementation "com.android.support.test:runner:$versions.testSupportLibrary"
    androidTestImplementation "com.android.support.test:rules:$versions.testSupportLibrary"
    androidTestImplementation "org.hamcrest:hamcrest-library:$versions.hamcrest"
}

apply from: project.file('../config/gradle/publishing.gradle')
